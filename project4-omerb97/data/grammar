### Sample grammar for ATIS queries
### CS187
### (Project segment 4 distribution version)

## This grammar is intended to cover ATIS flight ID queries only. It
## assumes that such queries are a single noun phrase, perhaps
## preceded and followed by other text that can be ignored. (The
## nonterminals PREIGNORE and POSTIGNORE capture this text.)
##
## Conventions used in this grammar:
##
## The start nonterminal is S. Other nonterminal names are typically
## syntactic phrase types (e.g., NP, PP, Det, P) followed by a
## semantic category (e.g., FLIGHT, AIRLINE), separated by an underscore.
## The phrase type TERM is used for lexical lists for a particular semantic
## category.
## 
## Augmentations for these grammar rules follow a standard convention for the 
## types of the values associated with different nonterminals. We document these
## conventions here:
##
## Values: SQL query (as a string), e.g.,
##   "SELECT flight.flight_id FROM flight WHERE true"
##
##     S
##
## Values: SQL condition (that is, appropriate for a WHERE clause body) 
## for a relation `flight`, e.g.,
##   "flight.airline_code = 'UA'"
##
##     NOM_FLIGHT  
##     N_FLIGHT
##     PP
##     PP_*
##     ADJ
##     ADJ_*
##     TERM_FLIGHT
##     etc.

S -> NP_FLIGHT  : lambda NP_FLIGHT: SNP(NP_FLIGHT)
        | PREIGNORE NP_FLIGHT   : lambda PREIGNORE, NP_FLIGHT: SNP(NP_FLIGHT)
        | NP_FLIGHT POSTIGNORE  : lambda NP_FLIGHT, POSTIGNORE: SNP(NP_FLIGHT)
        | PREIGNORE NP_FLIGHT POSTIGNORE        : lambda PREIGNORE, NP_FLIGHT, POSTIGNORE: SNP(NP_FLIGHT)

NP_FLIGHT -> DET NOM_FLIGHT     : second
        | NOM_FLIGHT            : first

NOM_FLIGHT -> N_FLIGHT          : first
        | ADJ NOM_FLIGHT        : lambda ADJ, NOM_FLIGHT: _and(ADJ, NOM_FLIGHT)

N_FLIGHT -> TERM_FLIGHT         : first
        | N_FLIGHT PP           : lambda N_FLIGHT, PP: _and(N_FLIGHT, PP)

TERM_FLIGHT -> 'flights'        : constant(' '.join(str(null_condition())))
        | 'flight'
        | 'to' 'fly'

## Pre-noun (primarily adjectival) flight modifiers
ADJ -> ADJ_AIRLINE      : first
        | ADJ_DATE
        | ADJ_FLIGHTTYPE
        | ADJ_TIME
        | ADJ_PLACE
        | ADJ_PRICE
        | ADJ_CLASS
        | ADJ_FOOD

## Post-noun (prepositional phrase and relative clause) flight modifiers
PP -> PP_AIRLINE        : first
        | PP_DATE
        | PP_TIME
        | PP_PLACE
        | PP_PRICE
        | PP_CLASS
        | PP_FOOD

DET -> 'all' 'the'      : ignore
        | 'all'
        | 'a'
        | 'an'
        | 'the'
        | 'any'
        | 'all' 'of' 'the'
        | 'this'
        | 'this' 'coming'
        | 'next'
        | TERM_WEEKDAY "'s"

#### FLIGHT PROPERTIES ####

## Airline information

ADJ_AIRLINE -> TERM_AIRLINE             : first

PP_AIRLINE -> P_AIRLINE TERM_AIRLINE    : second

P_AIRLINE -> 'on'       : ignore
        | 'using'
        | 'of'
        | 'with'

TERM_AIRLINE -> TERM_AIRBRAND           : lambda TERM_AIRBRAND: airline_code(TERM_AIRBRAND)
        | TERM_AIRBRAND TERM_AIRBRANDTYPE       : lambda TERM_AIRBRAND, TERM_AIRBRANDTYPE: airline_code(TERM_AIRBRAND)

TERM_AIRBRAND -> 'continental'        : constant('CO')
        | 'american'                  : constant('AA')
        | 'united'                    : constant('UA')
        | 'northwest'                 : constant('NW')
        | 'us'                        : constant('US')
        | 'delta'                     : constant('DL')
        | 'air' 'canada'              : constant('AC')
        | 'midwest' 'express'         : constant('YX') 
        | 'twa' | 'trans' 'world'     : constant('TW')

TERM_AIRBRANDTYPE -> 'airline'        : ignore
        | 'airlines'
        | 'air'

## Flight type information

ADJ_FLIGHTTYPE -> ADJ_FLIGHTTYPESIMPLE
        | ADJ_FLIGHTTYPESIMPLE 'and' ADJ_FLIGHTTYPE
        | ADJ_FLIGHTTYPESIMPLE 'or' ADJ_FLIGHTTYPE

ADJ_FLIGHTTYPESIMPLE -> 'round' 'trip'
        | 'round-trip'
        | 'return'
        | 'one' 'way'
        | 'nonstop'
        | 'direct'
        | 'connecting'

## Other properties (cost, class, food)

ADJ_PRICE -> 'cheapest' :ignore
        | 'lowest' 'cost'
        | 'least' 'expensive'
        | 'inexpensive'
        | 'cheap'

        | 'expensive'
        | 'highest' 'cost'
        | 'most' 'expensive'

PP_PRICE -> 'less' 'than' AMOUNT 'dollars'

        | 'with' 'the' 'lowest' 'fare'
        | 'the' 'cheapest' 'way' 'possible'

        | 'with' 'the' 'highest' 'fare'

ADJ_CLASS -> 'economy'          : economy
        | 'thrift' 'economy'    : economy
        | 'first' 'class'       : first_class
        | 'transcontinental'
        | 'available'
        | 'possible'

PP_CLASS -> ADJ_CLASS : first

ADJ_FOOD -> NP_FOOD     : first

PP_FOOD -> P_FOOD NP_FOOD

NP_FOOD -> 'dinner'             : constant('DINNER')
        | 'lunch'               : constant('LUNCH')
        | 'breakfast'           : constant('BREAKFAST')
        | 'a' 'meal'

P_FOOD -> 'serving'
        | 'with'

#### DATES ####

ADJ_DATE -> TERM_WEEKDAY        : first

PP_DATE -> P_DATE NP_DATE       : second
        | NP_DATE               : first

P_DATE -> 'on'                  : ignore
        | 'returning' 'on'
        | 'of'
        | 'for'
        | 'next'
        | 'the' 'next'
        | 'in' 'the' 'next'
        | 'of' 'next'
        | 'leaving'
        | 'departing'
        | 'departing' 'on'
        | 'which' 'leave'
        | 'leaving' 'on'

        | 'arriving'
        | 'arriving' 'on'
        | 'that' 'arrive' 'on'
        | 'which' 'arrive' 'on'
        | 'a' 'week' 'from'

NP_DATE -> TERM_WEEKDAY         : first
        | 'a' TERM_WEEKDAY      : first
        | 'this' TERM_WEEKDAY
        | 'this' 'coming'
        | TERM_WEEKDAY ADJ_TIME : lambda TERM_WEEKDAY, ADJ_TIME: and_(TERM_WEEKDAY, ADJ_TIME)
        | NP_MDYDATE            : first
        | 'saturdays'           : ignore
        | 'sundays'
        | 'mondays'
        | 'tuesdays'
        | 'wednesdays'
        | 'thursdays'
        | 'fridays'
        | 'day'
        | 'week'
        | 'today'
        | 'tomorrow'
        | 'the' 'day' 'after' 'tomorrow'
        | 'weekdays'

NP_MDYDATE -> TERM_MONTH TERM_DAY YEAR                                                  : lambda TERM_MONTH, TERM_DAY, YEAR: date_MDY(TERM_DAY, TERM_MONTH, YEAR)
        | TERM_MONTH TERM_DAY                                                           : lambda TERM_MONTH, TERM_DAY: date_MDY(TERM_DAY, TERM_MONTH)
        | 'the' TERM_DAY 'of' TERM_MONTH                                                : lambda TERM_DAY, TERM_MONTH: date_MDY(TERM_DAY, TERM_MONTH)
        | TERM_DAY 'of' TERM_MONTH                                                      : lambda TERM_DAY, TERM_MONTH: date_MDY(TERM_DAY, TERM_MONTH)
        | 'the' TERM_DAY                                                                : lambda TERM_DAY: date_MDY(TERM_DAY)
        | TERM_MONTH TERM_DAY 'or' TERM_DAY                                             : lambda TERM_MONTH, D1, D2: or_dates(D1, TERM_MONTH, None, D2, TERM_MONTH)
        | 'either' TERM_MONTH TERM_DAY 'or' TERM_DAY                                    : lambda TERM_MONTH, D1, D2: or_dates(D1, TERM_MONTH, None, D2, TERM_MONTH)
        | 'either' 'the' TERM_DAY 'or' 'the' TERM_DAY 'of' TERM_MONTH                   : lambda D1, D2, TERM_MONTH: or_dates(D1, TERM_MONTH, None, D2, TERM_MONTH)
        | 'the' TERM_DAY 'or' TERM_DAY                                                  : lambda D1, D2: or_dates(D1, None, None, D2)
        | 'either' 'the' TERM_DAY 'or' 'the' TERM_DAY                                   : lambda D1, D2: or_dates(D1, None, None, D2)
        | 'the' TERM_DAY 'of' TERM_MONTH 'or' 'the' TERM_DAY 'of' TERM_MONTH            : lambda D1, M1, D2, M2: or_dates(D1, M1, None, D2, M2)
        | 'either' 'the' TERM_DAY 'of' TERM_MONTH 'or' 'the' TERM_DAY 'of' TERM_MONTH   : lambda D1, M1, D2, M2: or_dates(D1, M1, None, D2, M2)
        
TERM_WEEKDAY -> 'saturday'            :  constant(weekday(_RHS[0]))
        | 'sunday'
        | 'monday'
        | 'tuesday'
        | 'wednesday'
        | 'thursday'
        | 'friday'
        | 'weekday'
        
TERM_MONTH -> 'january'               : constant(month_name(_RHS[0]))
        | 'february' 
        | 'march' 
        | 'april' 
        | 'may' 
        | 'june' 
        | 'july' 
        | 'august' 
        | 'september' 
        | 'october' 
        | 'november' 
        | 'december'
        
TERM_DAY -> 'first'                   : constant(1)
        | 'second'                    : constant(2)
        | 'third'                     : constant(3)
        | 'fourth'                    : constant(4)
        | 'fifth'                     : constant(5)
        | 'sixth'                     : constant(6)
        | 'seventh'                   : constant(7)
        | 'eighth'                    : constant(8)
        | 'ninth'                     : constant(9)
        | 'tenth'                     : constant(10)
        | 'eleventh'                  : constant(11)
        | 'twelfth'                   : constant(12)
        | 'thirteenth'                : constant(13)
        | 'fourteenth'                : constant(14)
        | 'fifteenth'                 : constant(15)
        | 'sixteenth'                 : constant(16)
        | 'seventeenth'               : constant(17)
        | 'eighteenth'                : constant(18)
        | 'nineteenth'                : constant(19)
        | 'twentieth'                 : constant(20)
        | 'twenty-first'              : constant(21)
        | 'twenty' 'first'            : constant(21)
        | 'twenty-second'             : constant(22)
        | 'twenty' 'second'           : constant(22)
        | 'twenty-third'              : constant(23)
        | 'twenty' 'third'            : constant(23)
        | 'twenty-fourth'             : constant(24)
        | 'twenty' 'fourth'           : constant(24)
        | 'twenty-fifth'              : constant(25)
        | 'twenty' 'fifth'            : constant(25)
        | 'twenty-sixth'              : constant(26)
        | 'twenty' 'sixth'            : constant(26)
        | 'twenty-seventh'            : constant(27)
        | 'twenty' 'seventh'          : constant(27)
        | 'twenty-eighth'             : constant(28)
        | 'twenty' 'eighth'           : constant(28)
        | 'twenty-ninth'              : constant(29)
        | 'twenty' 'ninth'            : constant(29)
        | 'thirtieth'                 : constant(30)
        | 'thirty-first'              : constant(31)
        | 'thirty' 'first'            : constant(31)
        | 'one'                       : constant(1)
        | 'two'                       : constant(2)
        | 'three'                     : constant(3)
        | 'four'                      : constant(4)
        | 'five'                      : constant(5)
        | 'six'                       : constant(6)
        | 'seven'                     : constant(7)
        | 'eight'                     : constant(8)
        | 'nine'                      : constant(9)
        | 'ten'                       : constant(10)
        | 'eleven'                    : constant(11)
        | 'twelve'                    : constant(12)
        | 'thirteen'                  : constant(13)
        | 'fourteen'                  : constant(14)
        | 'fifteen'                   : constant(15)
        | 'sixteen'                   : constant(16)
        | 'seventeen'                 : constant(17)
        | 'eighteen'                  : constant(18)
        | 'nineteen'                  : constant(19)
        | 'twenty'                    : constant(20)
        | 'twenty' 'one'              : constant(21)
        | 'twenty' 'two'              : constant(22)
        | 'twenty' 'three'            : constant(23)
        | 'twenty' 'four'             : constant(24)
        | 'twenty' 'five'             : constant(25)
        | 'twenty' 'six'              : constant(26)
        | 'twenty' 'seven'            : constant(27)
        | 'twenty' 'eight'            : constant(28)
        | 'twenty' 'nine'             : constant(29)
        | 'thirty'                    : constant(30)
        | 'thirty' 'one'              : constant(31)

TERM_YEAR -> '1991'                   : constant(int(_RHS[0]))
        | '1992'

#### TIMES ####

PP_TIME -> P_TIME NP_TIME               : forward

P_TIME -> 'that' 'arrive' 'before'      : lambda: arrive_before
        | 'that' 'arrives' 'before'
        | 'arriving' 'before'
        | 'arrival' 'by'
        | 'which' 'arrive' 'before'

        | 'departing' 'before'          : lambda: depart_before
        | 'before'
        | 'that' 'leaves' 'before'
        | 'by'
    
        | 'that' 'return' 'around'      : lambda: arrive_around
        | 'that' 'gets' 'in' 'around'
        | 'arriving' 'around'
        | 'arriving' 'about'
        | 'that' 'arrive' 'soon' 'after'

        | 'around'                      : ignore
        
        | 'arrives'                     : lambda: arrive_around
        | 'arriving' 'at'
        | 'arriving'

        | 'leaving' 'at'        : lambda: depart_around
        | 'at'
        | 'leaving'
        | 'in'
        | 'departing' 'at'
        | 'on'
        | 'that' 'leaves' 'at'
        
        | 'arriving' 'after'    : lambda: arrive_after
        | 'which' 'arrives' 'after'
        | 'that' 'arrives' 'after'

        | 'which' 'leave' 'after'       : lambda: depart_after
        | 'leaving' 'after'
        | 'after'
        | 'departing' 'after'
        | 'departing' 'in'
        | 'that' 'depart' 'after'
        | 'that' 'leaves' 'in'

NP_TIME -> TERM_TIME                  : first
        | 'afternoons'                : constant(1200)
        | 'the' 'afternoon'
        | 'the' 'late' 'afternoon'    : constant(1600)
        | 'evenings'                  : constant(1800)
        | 'the' 'evening'
        | 'mornings'                  : constant(800)
        | 'the' 'morning'
        | 'the' 'early' 'am' 
        | 'as' 'early' 'as' 'possible' 
        | 'earliest' 'possible' 'time' 
        | 'the' 'day'                 : constant(1200)
        | 'as' 'soon' 'thereafter' 'as' 'possible' 
        | UNK


ADJ_TIME -> 'lunch' 'time'            : constant(depart_around(1200))
        | 'evening'                   : constant(depart_around(1800))
        | 'dinnertime'
        | 'late' 
        | 'night'                     : constant(depart_around(2000))
        | 'latest' 'possible' 
        | 'latest'
        | 'tonight' 
        | 'morning'                   : constant(depart_around(800))
        | 'early' 
        | 'earliest' 'possible' 
        | 'earliest' 
        | 'afternoon'                 : constant(depart_around(1600))
        | SIMPLETIME                  : first

TERM_TIME -> 'one' | '1'              : constant(100)
        | 'two' | '2'                 : constant(200)
        | 'three' | '3'               : constant(300)
        | 'four' | '4'                : constant(400)
        | 'five' | '5'                : constant(500)
        | 'six' | '6'                 : constant(600)
        | 'seven' | '7'               : constant(700)
        | 'eight' | '8'               : constant(800)
        | 'nine' | '9'                : constant(900)
        | 'ten' | '10'                : constant(1000)
        | 'eleven' | '11'             : constant(1100)
        | 'twelve' | '12' | 'noon'    : constant(1200)
        | 'midnight'                  : constant(0)
        | TERM_TIME TERM_TIMEMOD      : lambda Time, Mod: Time + Mod

TERM_TIMEMOD -> 'am'                  : constant(0)
        | 'oclock'
        | 'o' "'clock"
        | 'o' 'clock'
        | 'noon' 
        | 'pm'                        : constant(1200)
        | 'midnight' 

#### PLACES ####

ADJ_PLACE -> N_PLACE                  : first

N_PLACE -> TERM_PLACE                 : first
        | TERM_AIRPORT

PP_PLACE -> P_PLACE N_PLACE             : forward       
        | 'between' N_PLACE 'and' N_PLACE       : lambda x, y: _and(from_airport(x), to_airport(y))


P_PLACE -> 'to'                         : lambda: to_airport
        | 'that' 'arrive' 'at'
        | 'that' 'arrives' 'in'
        | 'coming' 'back' 'to'
        | 'that' 'go' 'to'
        | 'and' 'then' 'to'
        | 'arriving' 'in'
        | 'and' 'arriving' 'in'
        | 'and' 'arrive' 'in'
        | 'to' 'arrive' 'in'
        | 'arrive' 'in'
        | 'going' 'to'
        | 'into'
        | 'for'
        | 'with' 'the' 'destination' 'city' 'of'
        | 'arriving'
        | 'goes' 'to'
        | 'flying' 'into'
        | 'goes' 'on' 'to'
        | 'reaching'
        | 'in'
        | 'and' 'then'
        | 'arriving' 'to'

        | 'from'                       : lambda: from_airport
        | 'leaving'
        | 'return' 'from'
        | 'leaving' 'from'
        | 'departing' 'from'
        | 'departing'
        | 'go' 'from'
        | 'going' 'from'
        | 'back' 'from'
        | 'that' 'goes' 'from'
        | 'that' 'departs'
        | 'which' 'leaves' 'from'
        | 'which' 'leave'
        | 'that' 'leave'
        | 'originating' 'in'
        | 'leave'
        | 'out' 'of'
        | 'leaves' 'from'
        | 'to' 'get' 'from'

        | 'via'                         : lambda: stop_airport
        | 'with' 'a' 'stopover' 'in'
        | 'with' 'a' 'layover' 'in'
        | 'with' 'a' 'stopover' 'at'
        | 'and' 'a' 'stopover' 'in'
        | 'stop' 'in'
        | 'stopping' 'in'
        | 'make' 'a' 'stop' 'in'
        | 'with' 'a' 'stop' 'in'
        | 'with' 'one' 'stop' 'in'
        | 'go' 'through'
        | 'which' 'go' 'through'
        | 'makes' 'a' 'stopover' 'in'
        | 'that' 'stops' 'in'
        | 'that' 'stops' 'over' 'in'
        | 'by' 'way' 'of'
        | 'connecting' 'through'
        | 'that' 'will' 'stop' 'in'
        | 'which' 'connects' 'in'
        | 'arriving' 'and' 'departing' 'at'

## Place terms includes city and airport names. The provided semantic augmentations
## return a SQL query that generates a list of airport codes for the place. For 
## airport names, like "logan", it generates a query like
## 
##    SELECT airport.airport_code 
##    FROM airport 
##    WHERE airport.airport_name = "LOGAN INTERNATIONAL"
##
## For city names, like "boston", it generates a query like
##
##    SELECT airport_service.airport_code 
##    FROM airport_service 
##    WHERE airport_service.city_code IN
##        (SELECT city.city_code 
##         FROM city 
##         WHERE city.city_name = "BOSTON")
##
## Note the use of the appropriate field values found in the ATIS database, which are 
## quoted uppercase strings like "LOGAN INTERNATIONAL" or "BOSTON"

TERM_AIRPORT -> TERM_AIRPORT 'airport'           : first
     | TERM_AIRPORT 'airports'

     | 'baltimore/washington' 'international'    : constant(airports_from_airport_name(' '.join(_RHS)))
     | 'burbank'
     | 'buttonville'
     | 'charlotte/douglas' 'international'
     | 'cincinnati/northern' 'kentucky' 'intl.'
     | 'dallas/fort' 'worth' 'international'
     | 'detroit' 'city'
     | 'dorval' 'international'
     | 'dulles' 'international'
     | 'general' 'mitchell' 'international'
     | 'greater' 'pittsburgh' 'international'
     | 'hobby'
     | 'hopkins' 'international'
     | 'houston' 'intercontinental'
     | 'indianapolis' 'international'
     | 'john' 'f.' 'kennedy' 'international'
     | 'kansas' 'city' 'international'
     | 'la' 'guardia'
     | 'lambert' 'st.' 'louis' 'international'
     | 'lester' 'b.' 'pearson' 'international'
     | 'lindbergh' 'field/san' 'diego' 'international'
     | 'logan' 'international'
     | 'long' 'beach' 'municipal'
     | 'los' 'angeles' 'international'
     | 'love' 'field'
     | 'mccarran' 'international'
     | 'memphis' 'international'
     | 'metropolitan' 'oakland' 'international'
     | 'metropolitan' 'wayne' 'county'
     | 'miami' 'international'
     | 'midway'
     | 'minneapolis/st.' 'paul' 'international'
     | 'mirabel'
     | 'nashville' 'international'
     | 'newark' 'international'
     | "o'hare" 'international'
     | 'ontario' 'international'
     | 'orlando' 'international'
     | 'philadelphia' 'international'
     | 'port' 'columbus' 'international'
     | 'salt' 'lake' 'city' 'international'
     | 'san' 'francisco' 'international'
     | 'san' 'jose' 'international'
     | 'seattle/tacoma' 'international'
     | 'sky' 'harbor' 'international'
     | 'st.' 'petersburg/clearwater' 'international'
     | 'stapleton' 'international'
     | 'tampa' 'international'
     | 'toronto' 'island'
     | 'washington' 'national'
     | 'westchester' 'county'
     | 'william' 'b.' 'hartsfield' 'atlanta' 'intl.'

     ## airport name synonyms
     | 'bwi'                           : constant(airports_from_airport_name('baltimore/washington international'))
     | 'douglas'                       : constant(airports_from_airport_name('charlotte/douglas international'))
     | 'dfw'                           : constant(airports_from_airport_name('dallas/fort worth international'))
     | 'dorval'                        : constant(airports_from_airport_name('dorval international'))
     | 'dulles'                        : constant(airports_from_airport_name('dulles international'))
     | 'general mitchell'              : constant(airports_from_airport_name('general mitchell international'))
     | 'hopkins'                       : constant(airports_from_airport_name('hopkins international'))
     | 'jfk'                           : constant(airports_from_airport_name('john f. kennedy international'))
         | 'john' 'f.' 'kennedy'
         | 'kennedy'
     | 'laguardia'                     : constant(airports_from_airport_name('la guardia'))
     | 'lambert'                       : constant(airports_from_airport_name('lambert st. louis international'))
     | 'pearson'                       : constant(airports_from_airport_name('lester b. pearson international'))
     | 'lindbergh'                     : constant(airports_from_airport_name('lindbergh field/san diego international'))
     | 'logan'                         : constant(airports_from_airport_name('logan international'))
     | 'mccarran'                      : constant(airports_from_airport_name('mccarran international'))


TERM_PLACE -> 'atlanta'                : constant(airports_from_city(' '.join(_RHS)))
        | 'austin' 
        | 'baltimore' 
        | 'boston' 
        | 'burbank' 
        | 'charlotte' 
        | 'chicago' 
        | 'cincinnati' 
        | 'cleveland' 
        | 'columbus' 
        | 'dallas' 
        | 'denver' 
        | 'detroit' 
        | 'houston'
        | 'indianapolis' 
        | 'kansas' 'city' 
        | 'las' 'vegas' 
        | 'long' 'beach' 
        | 'los' 'angeles' 
        | 'memphis' 
        | 'miami' 
        | 'milwaukee' 
        | 'minneapolis' 
        | 'montreal' 
        | 'nashville' 
        | 'new' 'york' 
        | 'newark'
        | 'oakland' 
        | 'ontario' 
        | 'orlando' 
        | 'philadelphia' 
        | 'phoenix'
        | 'pittsburgh' 
        | 'salt' 'lake' 'city'
        | 'san' 'diego'
        | 'san' 'francisco'
        | 'san' 'jose'
        | 'seattle'
        | 'st.' 'louis'
        | 'st.' 'paul'
        | 'st.' 'petersburg'
        | 'tacoma'
        | 'tampa' 
        | 'toronto' 
        | 'washington'
        | 'dc' 
        | 'westchester' 'county'

        ## city name synonyms
        | 'cleveland' 'ohio'          : constant(airports_from_city('cleveland'))
        | 'dallas' 'fort' 'worth'     : constant(airports_from_city('dallas'))
        | 'fort' 'worth'              : constant(airports_from_city('dallas'))
        | 'denver' 'colorado'         : constant(airports_from_city('denver'))
        | 'montreal' 'quebec'         : constant(airports_from_city('montreal'))
        | 'montreal' 'canada'         : constant(airports_from_city('montreal'))
        | 'new' 'york' 'city'         : constant(airports_from_city('new york')) 
        | 'newark' 'new' 'jersey'     : constant(airports_from_city('newark'))
        | 'oakland' 'california'      : constant(airports_from_city('oakland'))  
        | 'orlando' 'florida'         : constant(airports_from_city('new york'))  
        | 'philly'                    : constant(airports_from_city('philadelphia'))
        | 'san' 'diego' 'california'  : constant(airports_from_city('san diego'))
        | 'tacoma' 'washington'       : constant(airports_from_city('tacoma'))
        | 'washington' 'dc'           : constant(airports_from_city('washington'))
        | 'westchester'               : constant(airports_from_city('westchester')) 

#### IGNORABLE ####

PREIGNORE -> PREIGNORESYMBOL          : ignore
    | PREIGNORESYMBOL PREIGNORE
    
PREIGNORESYMBOL -> 'me'               : ignore
        | 'show' 
        | 'now' 
        | 'only' 
        | 'can' 
        | 'you' 
        | 'the' 
        | 'itinerary' 
        | 'of' 
        | 'also' 
        | 'a' 
        | 'list' 
        | 'could' 
        | 'give' 
        | 'which' 
        | 'what' 
        | 'is' 
        | "what's" 
        | 'are' 
        | 'my' 
        | 'choices' 
        | 'for' 
        | 'i' 
        | 'would' 
        | 'like' 
        | "i'd" 
        | 'to' 
        | 'see' 
        | 'have' 
        | 'make' 
        | 'book' 
        | 'find' 
        | 'information' 
        | 'on' 
        | 'know' 
        | 'some' 
        | 'hello' 
        | 'yes' 
        | 'please' 
        | 'repeat' 
        | 'do' 
        | 'have' 
        | 'there' 
        | 'need' 
        | 'hi' 
        | 'get' 
        | 'may' 
        | 'listing' 
        | 'listings' 
        | 'travel' 
        | 'arrangements' 
        | 'okay' 
        | 'want' 
        | 'tell' 
        | 'about' 
        | 'how' 
        | 'would' 
        | 'be' 
        | 'able' 
        | 'put' 
        | 'requesting' 
        | "i'm" 
        | 'looking' 
        | 'display' 

POSTIGNORE -> POSTIGNORESYMBOL               : ignore
        | POSTIGNORESYMBOL POSTIGNORE
    
POSTIGNORESYMBOL -> 'please'                 : ignore
        | 'there' 
        | 'are' 
        | 'currently' 
        | 'do' 
        | 'you' 
        | 'have' 
        | 'fares' 
        | 'information' 
        | 'i' 
        | 'want' 
        | 'would' 
        | 'like' 
        | 'the' 
        | 'flight' 
        | 'be' 
        | 'go' 
        | 'departures' 
        | 'is' 
        | 'such' 
        | 'a' 
        | 'that' 
        | 'serves' 
        | 'both' 
        | 'and' 
        | 'along' 
        | 'with' 
        | 'can' 
        | 'get' 
        | "i'd" 
        | 'traveling' 
        | 'for' 
        | 'me' 
        | UNK 
        | '.' 
        | '?'